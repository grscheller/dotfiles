#!/usr/bin/fish
#
# sfInstall: Installs system files into Arch Linux Systemd based computers.
#
#   Systemd Services & Timers used
#     - avahi-daemon.service
#     - dhcpcd.service
#     - iwd.service
#     - systemd-resolved,service
#     - reflector.timer
#

test (count $argv) -gt 0 || not fish_is_root_user
and begin
   printf '\nUsage: sudo $DOTFILES_GIT_REPO/bin/rootInstall\n\n'
   return 1
end

set -q DOTFILES_GIT_REPO
or set DOTFILES_GIT_REPO ~(echo $SUDO_USER)/devel/dotfiles

cd $DOTFILES_GIT_REPO 2>/dev/null
or begin
   printf '\nsfInstall: Failed to cd into the dotfiles'
   printf '\n           git directory \'%s\'\n\n' "$DOTFILES_GIT_REPO"
   return 1
end

test -d ./root-env
or begin
   printf '\nsfInstall: No \'%s\' directory, ...' "$DOTFILES_GIT_REPO/root-env"
   printf '\n           PUNTING!\n\n'
   return 1
end

set -l Files (string sub --start=9 (find root-env/ -type f))

# install my system configuration files
for File in $Files
   test -e $File
   or begin
      set -l Dir (string replace -r '/[^/]*$' '' $File)
      test -d $Dir
      or mkdir -p $Dir
      touch $File
   end
   cp root-env$File $File
end

# redo some symlinks - Pacman updates can step on these
rm /usr/bin/sh
ln -s -r /usr/bin/dash /usr/bin/sh
