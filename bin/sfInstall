#!/bin/fish
#
#  Install system files into my Wayland/Systemd based Arch Linux systems.
#
#  Systemd Services & Timers used
#    - avahi-daemon.service
#    - dhcpcd.service
#    - iwd.service
#    - nix-daemon.service
#    - systemd-resolved,service
#    - reflector.timer
#

test (count $argv) -gt 0 || not fish_is_root_user
and begin
   printf 'Usage: sudo ./sfInstall\n'
   printf '       sudo $DOTFILE_GIT_REPO/sfInstall\n'
   return 1
end

set -q DOTFILE_GIT_REPO
or set -gx DOTFILE_GIT_REPO ~grs/devel/dotfiles

cd $DOTFILE_GIT_REPO 2>/dev/null
or begin
   printf '\nFailed to cd into the dotfile repo\'s'
   printf '\nhome directory \'%s\'' "$DOTFILE_GIT_REPO"
   return 1
end

set -l Files (string sub --start=9 (find root-env/ -type f))

# install my system configuration files
for File in $Files
   test -e $File
   or begin
      set -l Dir (string replace -r '/[^/]*$' '' $File)
      test -d $Dir
      or mkdir -p $Dir
      touch $File
   end
   cp root-env$File $File
end

# redo some symlinks - pacman updates will step on these
rm /usr/bin/sh
ln -s -r /usr/bin/dash /usr/bin/sh
