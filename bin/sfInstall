#!/usr/bin/fish
#
# sfInstall: Installs system files into Arch Linux Systemd based computers.
#
#   Systemd Services & Timers used
#     - avahi-daemon.service
#     - dhcpcd.service
#     - iwd.service
#     - nix-daemon.service
#     - systemd-resolved,service
#     - reflector.timer
#
# TODO: use as template to create sfCheck fish script
#

test (count $argv) -gt 1 || not fish_is_root_user
and begin
   printf 'Usage: sudo $DOTFILES_GIT_REPO/bin/sfInstall [$DOTFILES_GIT_REPO]\n\n'
   return 1
end

test (count $argv) -eq 1
and begin
   set DOTFILES_GIT_REPO $argv[1]
end

set -q DOTFILES_GIT_REPO
or set DOTFILES_GIT_REPO ~(echo $SUDO_USER)/devel/dotfiles

cd $DOTFILES_GIT_REPO 2>/dev/null
or begin
   printf 'sfInstall: Failed to cd into the dotfiles\n'
   printf '           git directory \'%s\'\n\n' "$DOTFILES_GIT_REPO"
   return 1
end

test -d ./root-env
or begin
   printf 'sfInstall: No \'%s\' directory, ...\n' "$DOTFILES_GIT_REPO/root-env"
   printf '           PUNTING!\n\n'
   return 1
end

set -l Files (string sub --start=9 (find root-env/ -type f))

# install my system configuration files
for File in $Files
   test -e $File
   or begin
      set -l Dir (string replace -r '/[^/]*$' '' $File)
      test -d $Dir
      or mkdir -p $Dir
      touch $File
   end
   cp root-env$File $File
end

# redo some symlinks - pacman updates for Bash will step on these
rm /usr/bin/sh
ln -s -r /usr/bin/dash /usr/bin/sh
